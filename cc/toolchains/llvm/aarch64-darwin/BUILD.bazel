# Copyright (C) 2022 Swift Navigation Inc.
# Contact: Swift Navigation <dev@swift-nav.com>
#
# This source is subject to the license found in the file 'LICENSE' which must
# be be distributed together with this source. All other rights reserved.
#
# THIS CODE AND INFORMATION IS PROVIDED "AS IS" WITHOUT WARRANTY OF ANY KIND,
# EITHER EXPRESSED OR IMPLIED, INCLUDING BUT NOT LIMITED TO THE IMPLIED
# WARRANTIES OF MERCHANTABILITY AND/OR FITNESS FOR A PARTICULAR PURPOSE.

package(default_visibility = ["//visibility:public"])

load("//cc/toolchains/llvm:cc_toolchain_config.bzl", "cc_toolchain_config")

filegroup(
    name = "wrappers",
    srcs = glob([
        "wrappers/**",
    ]),
    visibility = ["//visibility:public"],
)

filegroup(
    name = "ar_files",
    srcs = [
        ":wrappers",
        "@aarch64-darwin-llvm//:ar",
    ],
)

filegroup(
    name = "as_files",
    srcs = [
        ":wrappers",
        "@aarch64-darwin-llvm//:as",
    ],
)

filegroup(
    name = "compiler_files",
    srcs = [
        ":wrappers",
        "@aarch64-darwin-llvm//:clang",
        "@aarch64-darwin-llvm//:include",
    ],
)

filegroup(
    name = "dwp_files",
    srcs = [
        ":wrappers",
        "@aarch64-darwin-llvm//:dwp",
    ],
)

filegroup(
    name = "linker_files",
    srcs = [
        ":wrappers",
        "@aarch64-darwin-llvm//:ar",
        "@aarch64-darwin-llvm//:clang",
        "@aarch64-darwin-llvm//:lib",
    ],
)

filegroup(
    name = "objcopy_files",
    srcs = [
        ":wrappers",
        "@aarch64-darwin-llvm//:objcopy",
    ],
)

filegroup(
    name = "strip_files",
    srcs = [
        ":wrappers",
        "@aarch64-darwin-llvm//:strip",
    ],
)

filegroup(
    name = "all_files",
    srcs = [
        "linker_files",
        ":compiler_files",
        "@aarch64-darwin-llvm//:bin",
    ],
)

cc_toolchain_config(
    name = "local-aarch64-darwin",
    abi_libc_version = "darwin_aarch64",
    abi_version = "darwin_aarch64",
    builtin_sysroot = "/Library/Developer/CommandLineTools/SDKs/MacOSX.sdk",
    compiler = "clang",
    cxx_builtin_include_directories = [
        "%sysroot%/usr/include",
        "%sysroot%/System/Library/Frameworks",
    ],
    host_system_name = "darwin-aarch64",
    is_darwin = True,
    target_cpu = "darwin",
    target_libc = "macosx",
    target_system_name = "aarch64-apple-macosx",
    tool_paths = {
        "ar": "wrappers/llvm-ar",
        "cpp": "wrappers/clang-cpp",
        "gcc": "wrappers/clang",
        "gcov": "wrappers/llvm-profdata",
        "llvm-cov": "wrappers/llvm-cov",
        "llvm-profdata": "wrappers/llvm-profdata",
        "ld": "/usr/bin/ld",
        "nm": "wrappers/llvm-nm",
        "objcopy": "wrappers/llvm-objcopy",
        "objdump": "wrappers/llvm-objdump",
        "strip": "wrappers/llvm-strip",
    },
    toolchain_identifier = "clang-aarch64-darwin",
    toolchain_path_prefix = "external/aarch64-darwin-llvm",
)

cc_toolchain(
    name = "cc-clang-aarch64-darwin",
    all_files = ":all_files",
    ar_files = ":ar_files",
    as_files = ":as_files",
    compiler_files = ":compiler_files",
    dwp_files = ":dwp_files",
    linker_files = ":linker_files",
    objcopy_files = ":objcopy_files",
    strip_files = ":strip_files",
    toolchain_config = ":local-aarch64-darwin",
)

toolchain(
    name = "cc-toolchain-aarch64-darwin",
    exec_compatible_with = [
        "@platforms//cpu:aarch64",
        "@platforms//os:macos",
    ],
    target_compatible_with = [
        "@platforms//cpu:aarch64",
        "@platforms//os:macos",
    ],
    target_settings = None,
    toolchain = ":cc-clang-aarch64-darwin",
    toolchain_type = "@bazel_tools//tools/cpp:toolchain_type",
)
